# 四、BuckleScript、Belt和互操作性

在这一章中，我们将更仔细地看看我们可以使用的特定于 BuckleScript 的特性。我们还将学习递归和递归数据结构。在这一章的结尾，我们将会对理性及其生态系统有一个完整的介绍。为此，我们将完成以下工作:

*   了解更多关于 Reason 模块系统的信息
*   探索了更多理性的原始数据结构(数组和列表)
*   了解各种管道操作符如何提高代码的可读性
*   熟悉原因和皮带标准库
*   已创建到 JavaScript 模块的绑定，以便在“原因”中使用
*   通过绑定到反应转换组组件，将路由转换添加到我们的应用中

为了跟随，使用任何你想要的环境。我们将要做的很多事情都不是特定于原因的。在这一章的最后，我们将继续构建我们的 ReasonReact 应用。

# 模块范围

正如你现在知道的，所有`.re`文件都是模块，所有模块都是全局可用的——包括嵌套的。默认情况下，通过提供命名空间，可以从任何地方访问所有类型和绑定。然而，一遍又一遍地这样做很快就会变得乏味。幸运的是，我们有一些方法可以让这变得更愉快:

```js
/* Foo.re */
type fromFoo =
  | Add(int, int)
  | Multiply(int, int);

let a = 1;
let b = 2;
```

接下来，我们将以不同的方式使用`Foo`模块的`fromFoo`类型以及它在另一个模块中的绑定:

*   **选项 1** :不加糖:

```js
/* Bar.re */
let fromFoo = Foo.Add(Foo.a, Foo.b);
```

*   **选项 2** :将模块别名为较短的名称。例如，我们可以声明一个新模块`F`并将其绑定到现有模块`Foo`:

```js
/* Bar.re */
module F = Foo;
let fromFoo = F.Add(F.a, F.b);
```

*   **选项 3** :使用`Module.()`语法在本地打开模块。此语法仅适用于单个表达式:

```js
/* Bar.re */
let fromFoo = Foo.(Add(a, b));
```

*   **选项 4** :在 OOP 意义上，让`Bar`使用`include`扩展`Foo`:

```js
/* Bar.re */
include Foo;
let a = 4; /* override Foo.a */
let fromFoo = Add(a, b);
```

*   **选项 5** :全球`open`模块。在大范围内谨慎使用`open`，因为很难知道哪些类型和绑定属于哪些模块:

```js
/* Bar.re */
open Foo;
let fromFoo = Add(a, b);
```

首选在本地范围内使用`open`:

```js
/* Bar.re */
let fromFoo = {
  open Foo;
  Add(a, b);
};
```

前面的语法将(通过`refmt`)重新格式化为选项 3 的语法，但是请记住，选项 3 的语法只适用于单个表达式。例如，以下内容无法转换为选项 3 的语法:

```js
/* Bar.re */
let fromFoo = {
  open Foo;
  Js.log("foo");
  let result = Add(a, b);
};
```

原因标准库可以在我们已经可用的各种模块中找到。例如，Reason 的标准库包含一个`Array`模块，我们可以使用点符号(即`Array.length`)来访问它的函数。

在[第 5 章](5.html)、*有效 ML* 中，我们将学习如何隐藏模块的类型和绑定，这样如果我们不希望它们是全局可用的，它们就不是全局可用的。

# 数据结构

我们已经看到了 Reason 的几种原始数据结构，包括字符串、整数、浮点、元组、记录和变体。让我们再探索一些。

# 排列

原因数组编译为常规 JavaScript 数组。原因数组如下:

*   同质(所有元素必须属于同一类型)
*   易变的
*   快速随机访问和更新

它们看起来像这样:

```js
let array = [|"first", "second", "third"|];
```

访问和更新数组的元素与在 JavaScript 中相同:

```js
array[0] = "updated";
```

在 JavaScript 中，我们映射数组，如下所示:

```js
/* JavaScript */
array.map(e => e + "-mapped")
```

为了在《理性》中做到这一点，我们有几个不同的选择。

# 使用原因标准库

原因标准库的`Array`模块包含几个函数，但不是所有你期望从 JavaScript 中得到的函数。然而，它确实有一个`map`功能:

```js
/* Reason standard library */
let array = [|"first", "second", "third"|];
Array.map(e => e ++ "-mapped", array);
```

`Array.map`的类型如下:

```js
('a => 'b, array('a)) => array('b);
```

类型签名表示`map`接受类型为`'a => 'b`的函数，类型为`'a`的数组，返回类型为`'b`的数组。注意，`'a`和`'b`是**型变量**。除了类型之外，类型变量就像普通变量。在上例中，`map`有一个类型:

```js
(string => string, array(string)) => array(string);
```

这是因为`'a`和`'b`类型的变量都一致地被具体的`string`类型所取代。

请注意，使用`Array.map`时，编译后的输出不会编译成 JavaScript 的`Array.prototype.map`—它有自己的实现:

```js
/* in the compiled output */
...
require("./stdlib/array.js");
...
```

The Reason standard library documentation can be found here:

[https://reasonml.github.io/api](https://reasonml.github.io/api)

# 使用皮带标准库

原因标准库实际上是 OCaml 标准库。它的创建并没有考虑到 JavaScript。“Belt”标准库是由创建BuckleScript的同一个人——张洪波——创建的，并附带了BuckleScript。Belt 是在考虑到 JavaScript 的情况下制作的，尤其以其性能而闻名。皮带标准库可通过`Belt`模块访问:

```js
/* Belt standard library */
let array = [|"first", "second", "third"|];
Belt.Array.map(array, e => e ++ "-mapped");
```

The Belt standard library documentation can be found here:

[https://bucklescript.github.io/bucklescript/api/Belt.html](https://bucklescript.github.io/bucklescript/api/Belt.html)

# 使用 BuckleScript 的内置 JavaScript 绑定

另一个很好的选择是使用 BuckleScript 的内置 JavaScript 绑定，可在`Js`模块中找到:

```js
/* BuckleScript's JavaScript bindings */
let array = [|"first", "second", "third"|];
Js.Array.map(e => e ++ "-mapped", array);
```

该选项的优点是在编译输出中不需要任何依赖关系。它还有一个非常熟悉的 API。然而，由于并非所有的原因数据结构都存在于 JavaScript 中，您可能会使用一个标准库。如果是这样，更喜欢Belt。

BuckleScript's binding documentation can be found here:

[https://bucklescript.github.io/bucklescript/api/Js.html](https://bucklescript.github.io/bucklescript/api/Js.html)

# 使用自定义绑定

没有什么能阻止您编写自己的自定义绑定:

```js
[@bs.send] external map: (array('a), 'a => 'b) => array('b) = "";
let array = [|"first", "second", "third"|];
map(array, e => e ++ "-mapped")
```

当然，您应该倾向于使用`Js`模块中的内置绑定。我们将在本章后面探讨更多的自定义绑定。

# 使用原始 JavaScript

最后的选择是在合理的范围内使用实际的 JavaScript:

```js
let array = [|"first", "second", "third"|];
let map = [%raw {|
  function(f, array) {
    return array.map(f)
  }
|}];
map(e => e ++ "-mapped", array)
```

BuckleScript 允许我们使用原始的 JavaScript，作为我们学习时保持高效的一种方式。当然，在这样做的时候，我们放弃了理智给我们提供的安全。所以，一旦你准备好了，把任何原始的 JavaScript 代码转换回更惯用的 Reason。

说到使用原始 JavaScript，表达式使用`%`，语句使用`%%`。请记住，`{| |}`是理性的多行字符串语法:

```js
let array = [%raw "['first', 'second', 'third']"];
[%%raw {|
  array = array.map(e => e + "-mapped");
|}];
```

使用原始表达式语法，我们还能够注释类型:

```js
let array: array(string) = [%raw "['first', 'second', 'third']"];
```

我们甚至可以注释函数类型:

```js
let random: unit => float = [%raw
  {|
    function() {
     return Math.random();
    }
  |}
];
```

尽管数组在来自 JavaScript 时很常见，但您可能会发现自己使用列表来代替，因为它们在函数式编程中无处不在。列表是不可变的，也是递归的。现在让我们看看如何使用这种递归数据结构。

# 目录

原因列表如下:

*   同质的
*   不变的
*   快速添加和访问列表头

它们看起来像这样:

```js
let list = ["first", "second", "third"];
```

在这种情况下，列表的头部是`"first"`。到目前为止，我们已经看到使用不可变的数据结构并不难。我们创造更新的副本，而不是突变。

当使用列表时，我们不能直接使用 JavaScript 绑定，因为列表在 JavaScript 中并不作为一种原始数据结构存在。但是，我们可以将列表转换为数组，反之亦然:

```js
/* Belt standard library */
let list = ["first", "second", "third"];
let array = Belt.List.toArray(list);

let array = [|"first", "second", "third"|];
let list = Belt.List.fromArray(array);

/* Reason standard library */
let list = ["first", "second", "third"];
let array = Array.of_list(list);

let array = [|"first", "second", "third"|];
let list = Array.to_list(array);
```

但是我们也可以直接映射一个列表:

```js
/* Belt standard library */
let list = ["first", "second", "third"];
Belt.List.map(list, e => e ++ "-mapped");

/* Reason standard library */
let list = ["first", "second", "third"];
List.map(e => e ++ "-mapped", list);
```

登录`list`到控制台显示列表在 JavaScript 中被表示为嵌套数组，其中每个数组总是有两个元素:

```js
["first", ["second", ["third", 0]]]
```

在理解列表是递归数据结构后，这是有意义的。原因列表是**单链表**。列表中的每个元素或者是空的**(在 JavaScript 中表示为`0`)或者是一个值和另一个列表的**组合**。**

 **`list`的示例类型定义揭示了`list`是一个变体:

```js
type list('a) = Empty | Head('a, list('a));
```

Note: type definitions can be recursive.

Reason 提供了一些语法糖，简化了它更冗长的版本:

```js
Head("first", Head("second", Head("third", Empty)));
```

# 递归

由于列表是一种递归数据结构，我们在使用它时通常使用递归。

为了热身，让我们编写一个(天真的)函数，对整数列表求和:

```js
let rec sum = list => switch(list) {
  | [] => 0
  | [hd, ...tl] => hd + sum(tl)
};
```

*   这是一个递归函数，因此需要`rec`关键字(即`let rec`而不仅仅是`let`)
*   我们可以在列表上进行模式匹配(就像任何其他变体和许多其他数据结构一样)
*   从示例类型定义来看，`Empty`表示为`[]`，`Head`表示为`[hd, ...tl]`，其中`hd`为列表的**头**，`tl`为列表的其余部分(即**尾**)
*   `tl`可能是`[]`(也就是`Empty`)，当它是时，递归停止

通过`sum`列表`[1, 2, 3]`导致以下步骤:

```js
sum([1, 2, 3])
1 + sum([2, 3])
1 + 2 + sum([3])
1 + 2 + 3
6
```

让我们通过分析另一个反转列表的(天真的)函数来更好地理解列表和递归:

```js
let rec reverse = list => switch(list) {
  | [] => []
  | [hd, ...tl] => reverse(tl) @ [hd]
};
```

*   同样，我们使用`rec`来定义递归函数
*   同样，我们在列表上使用模式匹配——如果它是空的，停止递归；否则，继续使用较小的列表
*   `@`运算符将第二个列表追加到第一个列表的末尾

传入先前定义的列表(`["first", "second", "third"]`)会导致以下步骤:

```js
reverse(["first", "second", "third"])
reverse(["second", "third"]) @ ["first"]
reverse(["third"]) @ ["second"] @ ["first"]
reverse([]) @ ["third"] @ ["second"] @ ["first"]
[] @ ["third"] @ ["second"] @ ["first"]
["third", "second", "first"]
```

这种逆向实现是幼稚的，原因有二:

*   不是尾调用优化(我们的`sum`函数也不是)
*   它使用`append` ( `@`)，比`prepend`慢

更好的实现是使用带有累加器的本地助手函数:

```js
let reverse = list => {
  let rec aux = (list, acc) => switch(list) {
    | [] => acc
    | [hd, ...tl] => aux(tl, [hd, ...acc])
  };
  aux(list, []);
};
```

现在，它的尾部调用被优化了，它使用了 prepend 而不是 append。在推理中，您使用`...`语法在列表前添加:

```js
let list = ["first", "second", "third"];
let list = ["prepended", ...list];
```

传入列表(`["first", "second", "third"]`)大致会导致以下步骤:

```js
reverse(["first", "second", "third"])
aux(["first", "second", "third"], [])
aux(["second", "third"], ["first"])
aux(["third"], ["second", "first"])
aux([], ["third", "second", "first"])
["third", "second", "first"]
```

请注意，在非尾部递归版本中，直到递归完成，Reason 才能创建列表。在尾部递归版本中，累加器(即`aux`的第二个参数)在每次迭代后都会更新。

尾部递归(即尾部调用优化)函数的好处是能够重用当前堆栈帧。因此，尾部递归函数永远不会有堆栈溢出，但非尾部递归函数可能会有堆栈溢出，只要迭代足够多。

# 管道操作员

原因有两个管道操作员:

```js
|> (pipe)
-> (fast pipe)
```

两个管道操作符都将参数传递给函数。`|>`管道操作符连接到函数的最后一个参数，而`->`快速管道操作符连接到函数的第一个参数。

看看这些:

```js
three |> f(one, two)
one -> f(two, three)
```

它们相当于:

```js
f(one, two, three)
```

如果函数只接受一个参数，那么两个管道的工作原理是一样的，因为函数的第一个参数也是函数的最后一个参数。

使用这些管道操作符非常流行，因为一旦掌握了窍门，它会让代码可读性更强。

我们不需要使用这个:

```js
Belt.List.(reduce(map([1, 2, 3], e => e + 1), 0, (+)))
```

我们可以用一种不需要读者从里到外阅读的方式来写:

```js
Belt.List.(
 [1, 2, 3]
 ->map(e => e + 1)
 ->reduce(0, (+))
);
```

如您所见，使用快速管道看起来类似于 JavaScript 中的链接。与 JavaScript 不同，我们可以传递 reduce`+`函数，因为它只是一个接受两个参数并添加它们的普通函数。圆括号是告诉 Reason 将中缀运算符`(+)`视为标识符所必需的。

# 使用皮带

让我们用本章到目前为止所学的内容来编写一个小程序，创建一副牌，洗牌，并从牌的顶部抽出五张牌。为此，我们将使用 Belt 的`Option`和`List`模块，以及快速管道操作器。

# 选项模块

传送带的`Option`模块是一组实用功能的集合，用于与`option`类型一起工作。例如，如果选项的值为`None`，要打开选项并抛出运行时异常，我们可以使用`getExn`:

```js
let foo = Some(3)->Belt.Option.getExn;
Js.log(foo); /* 3 */

let foo = None->Belt.Option.getExn;
Js.log(foo); /* raises getExn exception */
```

Belt functions that are able to throw runtime exceptions always have the `Exn` suffix.

打开不能抛出运行时异常的选项的另一个函数是`getWithDefault`:

```js
let foo = None->Belt.Option.getWithDefault(0);
Js.log(foo); /* 0 */
```

`Option`模块提供了其他几个功能，如`isSome`、`isNone`、`map`、`mapWithDefault`等。有关详细信息，请查看文档。

Belt Option module documentation can be found here:

[https://bucklescript.github.io/bucklescript/api/Belt.Option.html](https://bucklescript.github.io/bucklescript/api/Belt.Option.html)

# 列表模块

列表模块是列表数据类型的实用程序。要查看 Belt 为处理列表提供了哪些功能，请查看 Belt 的`List`模块文档。

Belt List module documentation can be found here:

[https://bucklescript.github.io/bucklescript/api/Belt.List.html](https://bucklescript.github.io/bucklescript/api/Belt.List.html)

让我们关注其中的几个。

# 制造

`make`功能用于创建填充列表。它接受列表长度的整数和列表中每个项目的值。其类型如下:

```js
(int, 'a) => Belt.List.t('a)
```

`Belt.List.t`被暴露为`list`类型的别名，所以我们可以说`Belt.List.make`的类型如下:

```js
(int, 'a) => list('a)
```

我们可以用它来创建一个包含十个字符串的列表，如下所示:

```js
let list = Belt.List.make(10, "string");
```

在[第 5 章](5.html)、*有效 ML* 中，我们将学习如何从模块中显式公开或隐藏类型和绑定。

# makeBy

`makeBy`函数类似于`make`函数，只是它接受一个函数，该函数用于确定给定项目索引的每个项目的值。

`makeBy`功能的类型如下:

```js
(int, int => 'a) => Belt.List.t('a)
```

我们可以用它来创建一个包含十个项目的列表，其中每个项目都等于它的索引:

```js
let list = Belt.List.makeBy(10, i => i);
```

# 洗牌

`shuffle`功能随机洗牌。它有类型:

```js
Belt.List.t('a) => Belt.List.t('a)
```

它接受一个列表并返回一个新列表。让我们用它来打乱整数列表:

```js
let list = Belt.List.(makeBy(10, i => i)->shuffle);
```

# 拿

`take`函数接受一个列表和一个长度，并从列表的头部开始返回列表的一个子集，其长度等于请求的长度。由于请求的子集长度可能超过原始列表的长度，因此结果被包装在一个选项中。其类型如下:

```js
(Belt.List.t('a), int) => option(Belt.List.t('a))
```

我们可以从洗牌后的列表中取出前两项，如下所示:

```js
let list = Belt.List.(makeBy(10, i => i)->shuffle->take(2));
```

# 一副牌示例

现在，我们准备将这一点与我们从前面章节中学到的内容结合起来。你将如何编写一个程序来创建一副牌，洗牌，并抽取前五张牌？在看下面的例子之前，你自己试一试。

```js
type suit =
  | Hearts
  | Diamonds
  | Spades
  | Clubs;

type card = {
  suit,
  rank: int,
};

Belt.List.(
  makeBy(52, i =>
    switch (i / 13, i mod 13) {
    | (0, rank) => {suit: Hearts, rank: rank + 1}
    | (1, rank) => {suit: Diamonds, rank: rank + 1}
    | (2, rank) => {suit: Spades, rank: rank + 1}
    | (3, rank) => {suit: Clubs, rank: rank + 1}
    | _ => assert(false)
    }
  )
  ->shuffle
  ->take(5)
  ->Belt.Option.getExn
  ->(
      cards => {
        let rankToString = rank =>
          switch (rank) {
          | 1 => "Ace"
          | 13 => "King"
          | 12 => "Queen"
          | 11 => "Jack"
          | rank => string_of_int(rank)
          };

        let suitToString = suit =>
          switch (suit) {
          | Hearts => "Hearts"
          | Diamonds => "Diamonds"
          | Spades => "Spades"
          | Clubs => "Clubs"
          };

        map(cards, ({rank, suit}) =>
          rankToString(rank) ++ " of " ++ suitToString(suit)
        );
      }
    )
  ->toArray
  ->Js.log
);
```

这产生了一个字符串格式的随机五张卡的数组:

```js
[
  "Queen of Clubs",
  "4 of Clubs",
  "King of Spades",
  "Ace of Hearts",
  "9 of Spades"
]
```

# 携带

一些 Belt 标准库函数有一个 *U* 后缀，比如这个:

```js
Belt.List.makeBy
```

您可以在这里看到后缀:

```js
Belt.List.makeByU
```

*U* 后缀代表*未结婚*。在进一步讨论之前，让我们先来定义一下讨好。

在推理中，每个函数只接受一个参数。这似乎与我们之前的许多例子相矛盾:

```js
let add = (a, b) => a + b;
```

前面的`add`函数看起来好像接受了两个参数，但实际上只是下面的语法糖:

```js
let add = a => b => a + b;
```

`add`函数接受单个参数`a`，返回接受单个参数的函数`b`，然后返回`a + b`的结果。

在推理中，两个版本都是有效的，并且具有相同的编译输出。在 JavaScript 中，前面两个版本都有效，但不一样；它们需要被不同地使用才能得到相同的结果。第二个需要这样称呼:

```js
add(2)(3);
```

这是因为`add`返回了一个需要再次调用的函数，因此有两组括号。理智会接受这两种用法:

```js
add(2, 3);
add(2)(3);
```

currying 的好处是它使编写函数更容易。您可以轻松创建部分应用的函数`addOne`:

```js
let addOne = add(1);
```

该`addOne`功能然后可以传递给其他功能，如`map`。也许您想使用这个特性将一个函数传递给 ReasonReact 子组件，并部分应用父组件的`self`。

令人困惑的是，`add`任一版本的编译输出如下:

```js
function add(a, b) {
  return a + b | 0;
}
```

中间函数在哪里？只要有可能，BuckleScript 都会优化编译后的输出，以避免不必要的函数分配，从而提高性能。

请记住，由于 Reason 的中缀运算符只是普通函数，我们可以只做以下事情:

```js
let addOne = (+)(1);
```

# 未实现的功能

由于 JavaScript 的动态特性，BuckleScript 不能总是优化编译后的输出来删除中间函数。但是，您可以使用以下语法告诉 BuckleScript 取消函数的限制:

```js
let add = (. a, b) => a + b;
```

uncurry 语法是参数列表中的点。它需要出现在声明和调用站点:

```js
let result = add(. 2, 3); /* 5 */
```

如果调用站点没有使用 uncurry 语法，BuckleScript 将引发编译时错误:

```js
let result = add(2, 3);

We've found a bug for you!

This is an uncurried BuckleScript function. It must be applied with a dot.

Like this: foo(. a, b)
Not like this: foo(a, b)
```

此外，如果调用位置缺少函数的某些参数，则会引发编译时错误:

```js
let result = add(. 2);

We've found a bug for you!

Found uncurried application [@bs] with arity 2, where arity 1 was expected.
```

术语`arity`是指函数接受的参数数量。

# makebu

如果我们解开它的第二个参数，我们可以用`makeByU`代替`makeBy`。这将提高性能(在我们的示例中可以忽略不计):

```js
...
makeByU(52, (. i) =>
  switch (i / 13, i mod 13) {
  | (0, rank) => {suit: Hearts, rank: rank + 1}
  | (1, rank) => {suit: Diamonds, rank: rank + 1}
  | (2, rank) => {suit: Spades, rank: rank + 1}
  | (3, rank) => {suit: Clubs, rank: rank + 1}
  | _ => assert(false)
  }
)
...
```

点语法要求在`i`周围加括号。

# JavaScript 互操作性

术语**互操作性**是指推理程序在推理中使用现有 JavaScript 的能力。BuckleScript 为在 Reason 中使用现有的 JavaScript 代码提供了一个很好的系统，也使得在 JavaScript 中使用 Reason 代码变得很容易。

# 在推理中使用 JavaScript

我们已经看到了如何在推理中使用原始的 JavaScript。现在让我们关注如何绑定到现有的 JavaScript。为了将一个值绑定到一个命名引用，我们通常使用`let`。然后，该绑定可以在后续代码中使用。当我们想要绑定到生命的价值是 JavaScript 时，我们使用`external`。`external`绑定就像一个`let`，可以在后续代码中使用。与`let`不同，`external`通常伴随着像`[@bs.val]`这样的巴克斯特装饰者。

# 了解[@bs.val]装饰器

我们可以使用`[@bs.val]`绑定全局值和函数。一般来说，语法如下:

```js
[@bs.val] external alert: string => unit = "alert";
```

*   一个或多个 BuckleScript 装饰者(即`[@bs.val]`)
*   `external`关键词
*   绑定的命名引用
*   类型声明
*   等号
*   一根绳子

外部关键字将`alert`绑定到类型为`string => unit`的值，并绑定到字符串`alert`。字符串`alert`是上述外部声明的值，也是将在编译输出中使用的内容。当外部绑定的名称等于其字符串值时，该字符串可以留空:

```js
[@bs.val] external alert: string => unit = "";
```

使用绑定就像使用任何其他绑定一样:

```js
alert("hi!");
```

# 理解[@bs.scope]装饰器

为了绑定到`window.location.pathname`，我们使用`[@bs.scope]`添加一个范围。这就定义了`[@bs.val]`的范围。例如，如果要绑定到`window.location`的`pathname`属性，可以将范围指定为`[@bs.scope ("window", "location")]`:

```js
[@bs.val] [@bs.scope ("window", "location")] external pathname: string = "";
```

或者，我们可以仅使用`[@bs.val]`将范围包含在字符串中:

```js
[@bs.val] external pathname: string = "window.location.pathname";
```

# 理解[@bs.send]装饰器

`[@bs.send]`装饰器用于绑定到对象的方法和属性。使用`[@bs.send]`时，第一个参数始终是对象。如果还有剩余的参数，它们将应用于对象的方法:

```js
[@bs.val] external document: Dom.document = "";
[@bs.send] external getElementById: (Dom.document, string) => Dom.element = "";
let element = getElementById(document, "root");
```

`Dom`模块也由 BuckleScript 提供，为 DOM 提供类型声明。

The Dom module documentation can be found here:

[https://bucklescript.github.io/bucklescript/api/Dom.html](https://bucklescript.github.io/bucklescript/api/Dom.html)

There is also a Node module for use with Node.js:

[https://bucklescript.github.io/bucklescript/api/Node.html](https://bucklescript.github.io/bucklescript/api/Node.html)

编写外部声明时要小心，因为您可能会意外地对类型系统撒谎，这可能会导致运行时类型错误。例如，我们告诉 Reason，我们的`getElementById`绑定总是返回一个`Dom.element`，但是当 DOM 找不到具有所提供的 ID 的元素时，它会返回`undefined`。更正确的绑定是这样的:

```js
[@bs.send] external getElementById: (Dom.document, string) => option(Dom.element) = "";
```

# 理解[@bs.module]装饰器

要导入节点模块，请使用`[@bs.module]`。编译后的输出取决于在`bsconfig.json`中使用的`package-specs`配置。我们使用`es6`作为模块格式。

```js
[@bs.module] external leftPad: (string, int) => string = "left-pad";
let result = leftPad("foo", 6);
```

这会编译为以下内容:

```js
import * as LeftPad from "left-pad";

var result = LeftPad("foo", 6);

export {
  result ,
}
```

将模块格式设置为`commonjs`会产生以下编译输出:

```js
var LeftPad = require("left-pad");

var result = LeftPad("foo", 6);

exports.result = result;
```

当`[@bs.module]`没有字符串参数时，导入默认值。

# 合理的原料药

当绑定到现有的 JavaScript 应用编程接口时，考虑一下您希望如何使用推理中的应用编程接口。即使是严重依赖 JavaScript 动态类型的现有 JavaScript APIs 也可以在 Reason 中使用。BuckleScript 利用先进的类型系统技术，让我们利用 Reason 的类型系统和这样的 API。

从 BuckleScript 文档中，看一下下面的 JavaScript 函数:

```js
function padLeft(value, padding) {
  if (typeof padding === "number") {
    return Array(padding + 1).join(" ") + value;
  }
  if (typeof padding === "string") {
    return padding + value;
  }
  throw new Error(`Expected string or number, got '${padding}'.`);
}
```

如果我们在 Reason 中绑定到这个函数，那么使用`padding`作为变体就好了。我们应该这样做:

```js
[@bs.val]
external padLeft: (
  string,
  [@bs.unwrap] [
    | `Str(string)
    | `Int(int)
  ])
  => string = "";

padLeft("Hello World", `Int(4));
padLeft("Hello World", `Str("Message: "));
```

这会编译为以下内容:

```js
padLeft("Hello World", 4);
padLeft("Hello World", "Message: ");
```

`padLeft`的类型是`(string, some_variant) => string`，其中`some_variant`使用了一个名为**多态变体**的高级类型系统特性，它使用`[@bs.unwrap]`来转换成 JavaScript 可以理解的东西。我们将在[第 5 章](5.html)、*有效 ML* 中了解更多多态变体。

# BuckleScript 文档

虽然这只是一个简单的介绍，但是你可以看到 BuckleScript 有很多工具可以帮助我们用惯用的 JavaScript 进行交流。我非常鼓励您阅读 BuckleScript 文档，以了解更多关于 JavaScript 互操作性的信息。

BuckleScript documentation can be found here:

[https://bucklescript.github.io/docs/interop-overview](https://bucklescript.github.io/docs/interop-overview)

# 绑定到现有 ReactJS 组件

反应组件不是原因组件。要使用现有的 ReactJS 组件，我们使用`[@bs.module]`导入节点模块，然后使用`ReasonReact.wrapJsForReason`助手函数将 ReactJS 组件转换为 react 组件。还有一个`ReasonReact.wrapReasonForJs`助手函数，用于在 ReactJS 中使用 ReactJS。

让我们从第 3 章、*中停止的地方继续构建我们的应用，创建推理推理组件:*

```js
git clone https://github.com/PacktPublishing/ReasonML-Quick-Start-Guide.git
cd ReasonML-Quick-Start-Guide
cd Chapter03/app-end
npm install
```

在这里，我们将通过绑定到现有的反应过渡组组件来添加路线过渡:

React Transition Group documentation can be found here:

[https://reactcommunity.org/react-transition-group/](https://reactcommunity.org/react-transition-group/)

# 正在导入依赖项

运行`npm install --save react-transition-group`安装依赖项。

让我们创建一个名为`ReactTransitionGroup.re`的新文件来存放这些绑定。在这个文件中，我们将绑定到`TransitionGroup`和`CSSTransition`组件:

```js
[@bs.module "react-transition-group"]
external transitionGroup: ReasonReact.reactClass = "TransitionGroup";

[@bs.module "react-transition-group"]
external cssTransition: ReasonReact.reactClass = "CSSTransition";
```

# 创建 make 函数

接下来，我们创建组件所需的`make`函数。这就是我们使用`ReasonReact.wrapJsForReason`助手功能的地方。

对于`TransitionGroup`，我们不需要任何道具。由于需要`~props`参数，我们通过`Js.Obj.empty()`。`~reactClass`参数通过我们在上一步中创建的外部绑定传递:

```js
module TransitionGroup = {
  let make = children =>
    ReasonReact.wrapJsForReason(
      ~reactClass=transitionGroup,
      ~props=Js.Obj.empty(),
      children,
    );
};
```

现在，`ReactTransitionGroup.TransitionGroup`是一个可以在我们的应用中使用的 ReasonReact 组件。

# 使用[@bs .派生类摘要]

`CSSTransitionGroup`将需要以下道具:

*   `_in`
*   `timeout`
*   `classNames`

由于`in`是 Reason 中的保留字，约定是在 Reason 中使用`_in`，让 BuckleScript 使用`[@bs.as "in"]`编译到`in`进行 JavaScript。

BuckleScript 提供了`[@bs.deriving abstract]`来轻松处理特定类型的 JavaScript 对象。我们可以直接使用 BuckleScript 创建该对象，而不是用 JavaScript 创建一个对象并绑定到该对象:

```js
[@bs.deriving abstract]
type cssTransitionProps = {
  [@bs.as "in"] _in: bool,
  timeout: int,
  classNames: string,
};
```

Note: `cssTransitionProps` is not a record type, it just looks like one.

使用`[@bs.deriving abstract]`时，会自动提供一个辅助函数来创建该形状的 JavaScript 对象。这个助手功能也被命名为`cssTransitionProps`。我们在组件的`make`函数中使用这个助手函数来创建组件的道具:

```js
module CSSTransition = {
  let make = (~_in: bool, ~timeout: int, ~classNames: string, children) =>
    ReasonReact.wrapJsForReason(
      ~reactClass=cssTransition,
      ~props=cssTransitionProps(~_in, ~timeout, ~classNames),
      children,
    );
};
```

# 使用组件

现在，在`App.re`中，我们可以改变渲染函数来使用这些组件。我们会改变这一点:

```js
<main> {currentRoute.component} </main>
```

现在显示如下:

```js
<main>
  ReactTransitionGroup.(
    <TransitionGroup>
      <CSSTransition
        key={currentRoute.title} _in=true timeout=900 classNames="routeTransition">
        {currentRoute.component}
      </CSSTransition>
    </TransitionGroup>
  )
</main>
```

Note: The key prop is a special ReactJS prop and should not be part of the component's props argument in `ReasonReact.wrapJsForReason`. The same is true for the special ReactJS ref prop.

为完整起见，这里是对应的 CSS，可以在`ReactTransitionGroup.scss`中找到:

```js
@keyframes enter {
  from {
    opacity: 0;
    transform: translateY(50px);
  }
}

@keyframes exit {
  to {
    opacity: 0;
    transform: translateY(50px);
  }
}

.routeTransition-enter.routeTransition-enter-active {
  animation: enter 500ms ease 400ms both;
}

.routeTransition-exit.routeTransition-exit-active {
  animation: exit 400ms ease both;
}
```

务必在`ReactTransitionGroup.re`中要求前面的内容:

```js
/* ReactTransitionGroup.re */
[@bs.val] external require: string => string = "";
require("../../../src/ReactTransitionGroup.scss");
```

现在，当改变路线时，旧路线的内容在新路线的内容变得活跃和淡入之前变得活跃和淡入。

# 摘要

BuckleScript 非常强大，因为它让我们能够以非常愉快的方式与惯用的 JavaScript 进行互操作。它还提供了Belt标准库，该库是在考虑到 JavaScript 的情况下创建的。我们了解了数组和列表，看到了在 ReactJS 中使用现有组件是多么容易。

在 [第 5 章](5.html)*有效 ML* 中，我们将学习如何在构建自动完成输入组件时使用模块签名来隐藏组件的实现细节。我们首先从硬编码的数据开始，在[第 6 章](6.html)、 *CSS-in-JS (in Reason)* 中，我们会将该数据移动到`localStorage`(客户端 web 存储)。**